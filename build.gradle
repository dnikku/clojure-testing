import org.gradle.api.publish.tasks.GenerateModuleMetadata

plugins {
    id "java-library"
    id 'maven-publish'

    id "dev.clojurephant.clojure" version "0.6.0"
    id 'com.adarshr.test-logger' version '3.2.0'
}


dependencies {
    implementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    implementation 'org.clojure:clojure:1.11.1'
    implementation 'org.clojure:tools.namespace:1.1.0'
    implementation 'lambdaisland:deep-diff2:2.0.108'

    devImplementation 'nrepl:nrepl:0.8.3'
    devImplementation 'cider:cider-nrepl:0.27.2'

    // testing
}

repositories {
    mavenCentral()
    maven {
        name = 'clojars'
        url = 'https://repo.clojars.org'
    }
}


defaultTasks 'assemble'


tasks.withType(Test) {
    useJUnitPlatform()

    // for reporting, see: https://medium.com/@wasyl/pretty-tests-summary-in-gradle-744804dd676c
}


/*
clojure {
    builds {
        main {
            aotAll()
            reflection = 'warn'
        }
    }
}*/


clojureRepl {
    port = 8095
    handler = 'cider.nrepl/cider-nrepl-handler'
}


gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }
}


java {
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "clojars"
            url = "https://repo.clojars.org"

            credentials {
                username = System.getenv("CLOJARS_USER")
                password = System.getenv("CLOJARS_TOKEN")
            }
        }
    }


    publications {
        maven(MavenPublication) {
            groupId = 'org.clojars.dnikku'
            artifactId = 'clojure-testing'
            version = '0.1'

            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'clojure-testing'
                description = 'A library that allow you to run clojure.test from a gradle project via junit-jupiter besides java tests.'
                url = 'https://github.com/dnikku/clojure-testing'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://raw.githubusercontent.com/dnikku/clojure-testing/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'dnikku'
                        name = 'Nicu Dascalu'
                        email = 'dnikku@gmail.com'
                    }
                }
                scm {
                    connection = 'https://github.com/dnikku/clojure-testing.git'
                    developerConnection = 'git@github.com:dnikku/clojure-testing.git'
                    url = 'https://github.com/dnikku/clojure-testing'
                }
            }

        }
    }
}

// // Clojars doesn"t support module metadata
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}